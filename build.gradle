buildscript {
    ext {
        springBootVersion = '3.2.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.1.4.RELEASE"
    }
}

// 하위 모든 프로젝트 공통 세팅
subprojects {
    group 'com.won'
    version '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '17'
    compileJava.options.encoding = 'UTF-8'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-aop'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        // lombok - mapstruct
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0' // v1.18.16+ 부터

        // mapstruct
        implementation 'org.mapstruct:mapstruct:1.4.2.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // test : h2
        testImplementation 'com.h2database:h2'

        // test : embedded-redis
        // implementation 'it.ozimov:embedded-redis:0.7.2'
    }

    test {
        useJUnitPlatform()
    }
}

project(':book-common') {
    bootJar { enabled = false } // common : bootJar 패키징 할 필요 없음
    jar { enabled = true }

    dependencies {
    }
}

project(':book-domain') {
    bootJar { enabled = false } // domain : bootJar 패키징 할 필요 없음
    jar { enabled = true }

    dependencies {
        api project(':book-common')
    }
}

project(':book-app-api') {
    dependencies {
        api project(':book-common')
        api project(':book-domain')
    }
}
